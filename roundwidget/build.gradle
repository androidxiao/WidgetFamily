apply plugin: 'com.android.library'
def config=rootProject.ext.android
def roundWidget=rootProject.ext.customWidget
def libs=rootProject.ext.dependencies
// 显示的时间格式，形如20190111
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault())
}
android {
    compileSdkVersion config.compileSdkVersion
    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode roundWidget.roundWidgetVersionCode
        versionName roundWidget.roundWidgetVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "roundWidget_release_V${defaultConfig.versionName}.${releaseTime()}.aar"
        }
    }

    repositories{
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.supportV7
    implementation libs.constraintlayout
    implementation (name:'xutils_release_V1.0.0.20190113',ext:'aar')

}

/** 以下开始是将Android Library上传到jcenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/androidxiao/WidgetFamily'    // project homepage
//项目的版本控制地址
def gitUrl = 'git@github.com:androidxiao/WidgetFamily.git' // project git

//发布到组织名称名字，必须填写
group = "cn.round.widget"
//发布到JCenter上的项目名字，必须填写
def libName = "RoundWidget"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0"
/**  上面配置后上传至jcenter后的编译路径是这样的： compile 'cn.round.widget:roundwidget:1.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/androidxiao/WidgetFamily/FastDev/tree/master/mylibrary/docs/javadoc"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'This is RoundWidget Family'
                url siteUrl
                licenses {
                    license {
                        name 'This is RoundWidget Family'
                        url 'https://github.com/androidxiao/WidgetFamily'
                    }
                }
                developers {
                    developer {
                        id 'kotyo-wei'
                        name 'kotyo-wei'
                        email 'zhawei1993@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'This is RoundWidget Family'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}